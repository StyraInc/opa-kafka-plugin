plugins {
    id 'scala'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.bisnode.opa' version '0.3.2'
}

group 'org.openpolicyagent.kafka'
version '1.5.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

// See versions used in Kafka here https://github.com/apache/kafka/blob/4.0.0/gradle/dependencies.gradle
dependencies {
    compileOnly group: 'org.apache.kafka', name: 'kafka_2.13', version: '4.0.0'
    compileOnly group: 'com.typesafe.scala-logging', name: 'scala-logging_2.13', version: '3.9.5'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-scala_2.13', version: '2.16.2'
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.2.0'

    testImplementation group: 'org.scalatest', name: 'scalatest_2.13', version: '3.2.17'
    testImplementation group: 'org.scalatestplus', name: 'junit-4-13_2.13', version: '3.2.17.0'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.0'
    testImplementation group: 'org.apache.kafka', name: 'kafka_2.13', version: '4.0.0'
    testImplementation group: 'org.apache.kafka', name: 'kafka-server', version: '4.0.0'
    testImplementation group: 'com.typesafe.scala-logging', name: 'scala-logging_2.13', version: '3.9.5'
}

shadowJar {
    dependencies {
        exclude(dependency {
            !(it.moduleGroup in ['org.openpolicyagent.kafka', 'com.github.ben-manes.caffeine']
                    || (it.moduleGroup == 'com.fasterxml.jackson.module' && it.moduleName == 'jackson-module-scala_2.13')
                    || (it.moduleGroup == 'com.thoughtworks.paranamer' && it.moduleName == 'paranamer'))
        })
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId ='org.openpolicyagent.kafka'
            artifactId = 'opa-authorizer'
            version = '1.5.1'

            from components.java

            pom {
                name = 'Open Policy Agent plugin for Kafka authorization'
                description = 'Open Policy Agent (OPA) plugin for Kafka authorization.'
                url = 'https://github.com/anderseknert/opa-kafka-plugin'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Anders Eknert'
                        email = 'anders@eknert.com'
                        organization = 'Styra'
                        organizationUrl = 'https://www.styra.com'
                    }
                    developer {
                        name = 'Jakub Scholz'
                        email = 'jakub@scholz.cz'
                        organization = 'Red Hat'
                        organizationUrl = 'https://www.redhat.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/anderseknert/opa-kafka-plugin.git'
                    developerConnection = 'scm:git:ssh://github.com/anderseknert/opa-kafka-plugin.git'
                    url = 'https://github.com/anderseknert/opa-kafka-plugin.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'OSSRH'
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

check.dependsOn(testRego, testRegoCoverage)
test.dependsOn startOpa
test.finalizedBy stopOpa
test.outputs.upToDateWhen {
    // Consider alternatives to this: https://stackoverflow.com/a/52484259
    false
}
